buildscript {
  ext.getExtOrDefault = { name, defValue ->
    rootProject.ext.has(name) ? rootProject.ext.get(name) : defValue
  }
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:8.3.2"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.21"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"
if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

android {
  if (project.android.hasProperty('namespace')) {
    namespace "com.cleveradssolutions.plugin.reactnative"
  }

  compileSdkVersion getExtOrDefault("compileSdkVersion", 35)

  defaultConfig {
    minSdkVersion getExtOrDefault("minSdkVersion", 23)
    targetSdkVersion getExtOrDefault("targetSdkVersion", 35)

    buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
  }

  buildFeatures { buildConfig true }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += ['src/newarch']
      } else {
        java.srcDirs += ['src/oldarch']
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    disable 'GradleCompatible'
    abortOnError false
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  implementation "com.facebook.react:react-android"

  implementation "com.cleveradssolutions:cas-sdk:4.5.0"
}
